hostname := `scutil --get LocalHostName`
username := `whoami`
workspace := "${HOME}/Projects/Workspace"

default:
  @just -g -l

# ╔═══════════════════════════════╗
# ║            chezmoi            ║
# ╚═══════════════════════════════╝

# Show chezmoi diff
[group('chezmoi')]
diff:
  chezmoi diff

# Apply chezmoi changes
[group('chezmoi')]
apply:
  chezmoi apply

# Re-add files to chezmoi
[group('chezmoi')]
readd:
  chezmoi re-add

# ╔═══════════════════════════════╗
# ║              Git              ║
# ╚═══════════════════════════════╝

# Create a new GitHub repo
[group('git')]
init owner repo visibility:
  #!/usr/bin/env bash
  set -euo pipefail
  gh repo create {{ owner }}/{{ repo }} {{ visibility }} --add-readme
  mkdir -p "${HOME}/Projects/{{ repo }}"
  cd "${HOME}/Projects/{{ repo }}"
  git init
  git remote add origin "https://github.com/{{ owner }}/{{ repo }}.git"
  git pull origin main

# Update and prune git branches
[group('git')]
prune:
  #!/usr/bin/env bash
  git fetch --all --prune
  git pull
  git branch --merged=main | grep -v main | grep -v '*' | xargs git branch -d

# ╔═══════════════════════════════╗
# ║              Nix              ║
# ╚═══════════════════════════════╝

# Apply nix-darwin configuration
[group('nix')]
darwin:
  sudo darwin-rebuild switch --flake ~/.config/nix#{{ hostname }}

# Apply garbage collection to nix store
[group('nix')]
gc:
  nix store gc

# ╔═══════════════════════════════╗
# ║            python             ║
# ╚═══════════════════════════════╝

# Launch IPython in global virtual environment
[group('python')]
i:
  #!/usr/bin/env bash
  source "{{ workspace }}/.venvs/global/.venv/bin/activate"
  ipython

# ╔═══════════════════════════════╗
# ║             utils             ║
# ╚═══════════════════════════════╝

# Update dotfiles and apply nix-darwin configuration
[group('utils')]
update:
  @just -g diff
  @just -g apply
  @just -g darwin
  @just -g gc
